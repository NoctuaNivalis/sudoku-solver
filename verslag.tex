\documentclass[a4paper,12pt]{article}

\usepackage{fullpage}
\usepackage{listings}
\usepackage{color}
\usepackage{lstautogobble}

\setlength{\parindent}{0pt}
\setlength{\parskip}{12pt}

\definecolor{mygray}{rgb}{0.4,0.4,0.4}
\definecolor{mymauve}{RGB}{235,225,223}
\definecolor{mygreen}{RGB}{0,225,0}

\lstset{ %
  backgroundcolor=\color{white},
  basicstyle=\ttfamily,
  breakatwhitespace=true,
  breaklines=true,
  captionpos=b,
  commentstyle=\color{mygreen},
  keepspaces=true,
  keywordstyle=\color{blue},
  language=Haskell,
  morekeywords={data},
  numbers=left,
  numbersep=5pt,
  numberstyle=\tiny\color{mygray},
  rulecolor=\color{black},
  showspaces=false,
  showstringspaces=false,
  showtabs=false,
  stepnumber=1,
  stringstyle=\color{mymauve},
  tabsize=2,
  title=\lstname,
  autogobble=true
}

\author{Felix Van der Jeugt}
\title{
    Programming Languages Assignments
}

\begin{document}



\maketitle



\begin{section}*{Preface}

    Before I begin my actual report, I would explain my choice of programming
    language. I have used \textit{Haskell} for my entire project. I've used
    this language before, so it seemed right to extends my knowledge of it with
    concurrency.

\end{section}


\begin{section}*{Assignment 1 - Declarative versus Stateful Model}


    \begin{subsection}*{The Solver}


        \begin{subsubsection}*{Choice of algorithms}

            For the linear solver of Sudoku's, a recursive backtracking solution seemed the
            best choice. As there are no other thread to communicate with, we can just pass
            around the Sudoku as an argument, never using state.

            However, once my backtracking program was finished, it seemed quite slow. I
            was able to speed it up a lot by preprocessing the Sudoku, scratching all
            contradicting solutions.

        \end{subsubsection}


        \begin{subsubsection}*{Choice of data structures}

            I modeled a Sudoku as an associative structure. By mapping tiles, a two
            dimensional location, on cells, which contains the digits, I can access the data
            in logarithmic complexity.

            In Haskell, this is noted as follows:

            \begin{lstlisting}
                type Sudoku = M.Map Tile Cell
                data Tile = Tile Int Int
                    deriving (Eq, Show, Ord)
                data Cell = E [Int] | D Int | X
                    deriving (Eq)
            \end{lstlisting}

            As you can see in the code, I've enable a \texttt{Cell} to either hold a list of
            possibilities, a decided digit, or an X. This \texttt{X} indicates either an
            invalid content, or is used as a placeholder.

        \end{subsubsection}


        \begin{subsubsection}*{Parsing the Sudoku}

            After modeling the Sudoku and thinking about the algorithms, I wrote a parser.
            This would simplify and speed up the process of testing and debugging.

            \begin{lstlisting}
            parseSudoku :: IO Sudoku
            parseSudoku = do
                lines <- replicateM 9 getLine
                return $ M.fromList
                    [ (Tile r c, parseCell char)
                    | (r, line) <- zip [0..] lines
                    , (c, char) <- zip [0..] line ]
              where
                parseCell char | isNumber char  = D $ digitToInt char
                               | otherwise      = E [1..9]
            \end{lstlisting}

            This will read Sudoku's from \texttt{stdin}, which are formatted like this:

            \begin{verbatim}
            ----135--
            ---6-7---
            ------2-1
            -9-----65
            5-------7
            28-----9-
            6-7------
            ---7-9---
            --482----
            \end{verbatim}

            I simply read all the characters, parsing them as a decided digit (\texttt{D (
            digitToInt char)}) if the character is a number, and parsing them as either of 1
            to 9 (\texttt{E [1..9]}) if it's not a number. With the \texttt{zip} function I
            index first each line (row), and then each character in that line.

        \end{subsubsection}


        \begin{subsubsection}*{Avoiding clashes}

            Next comes the most important function in my program,
            \texttt{fixTile}. This function takes a tile, assumes it's a correct
            decided digit, and removes all possibilities contradicting this from
            the Sudoku.

            \begin{lstlisting}
            fixTile :: Tile -> Sudoku -> Maybe Sudoku
            fixTile (Tile r c) s = validate $ M.mapWithKey (filterCell $ s M.! Tile r c) s
            \end{lstlisting}

            As you can see, the method returns a \texttt{Maybe Sudoku}, which
            means that it will return either \texttt{Just} a Sudoku, or
            \texttt{Nothing}. This is a way of indicating the function can fail,
            in this case when two decided digits contradict each other.

            Inside this function, \texttt{M.mapWithKey} is used. This is a built
            in function that will reform a Map. As parameter it takes a
            function which it will call on all entries in the map. The results of
            these functions will form the new Map. We validate the resulting
            Map, which means we check if there were no contradictions.

            \begin{lstlisting}
            filterCell :: Cell -> Tile -> Cell -> Cell
            filterCell (D a) t (D b)
              | a == b && dangerTile t  = X
              | otherwise               = D b
            filterCell (D a) t (E ds)
              | dangerTile t            = E (filter (/= a) ds)
              | otherwise               = E ds
            filterCell (E ds) _ c       = c
            \end{lstlisting}

            This function filters the decided digit from the other cells. It
            takes as parameters the cell of the decided digit, the location of
            the cell we're filtering and the cell we're filtering.

            The last line takes care of people who call this function with an
            undecided cell: It ignores them. The first match (line 2-4) checks
            that if both are decided cells and on the same row/column/box, they
            do not contradict each other. If they do, it places an \texttt{X},
            so the \texttt{validate} can see this.

            The second match is for undecided cells. If they lay in the same
            row, column or box of this cell, we filter the decided digit from
            it's possibilities.

        \end{subsubsection}


        \begin{subsubsection}*{The Preprocessing}

            When you've obtained the \texttt{fixTile} function, the
            preprocessing isn't that hard. You just call \texttt{fixTile} once
            for every tile, repeating this until there is no more change in the
            Sudoku:
            \begin{lstlisting}
            iterateFixes :: Sudoku -> Maybe Sudoku
            iterateFixes sudoku = F.foldrM fixTile sudoku (M.keys sudoku)

            fixSingles :: Sudoku -> Sudoku
            fixSingles = M.map go
              where
                go (E [n]) = D n
                go x       = x

            fillSudoku :: Sudoku -> Sudoku
            fillSudoku sudoku = fst (until noChange nextIteration (M.empty, sudoku))
              where
                noChange (a, b) = a == b
                nextIteration (_, old) = (old, fixSingles $ fromJust $ iterateFixes old)
            \end{lstlisting}

            We've got \texttt{iterateFixes} which calls \texttt{fixTile} for
            every tile (\texttt{M.keys sudoku}) and \texttt{fixSingles}, which
            makes a decided digit from every list of possibilities with only one
            option left. \texttt{fillSudoku} iterates these two until there is
            no more change.

        \end{subsubsection}


        \begin{subsubsection}*{The backtracking}

            What's left is the backtracking part of the program. It's called
            \texttt{solveSudoku}:
            \begin{lstlisting}
            solveSudoku :: Sudoku -> Maybe Sudoku
            solveSudoku = go (Tile 0 0) . return
              where
                go :: Tile -> Maybe Sudoku -> Maybe Sudoku
                go _ Nothing = Nothing
                go (Tile r c) (Just s)
                  | c >= 9      = go (Tile (r + 1) 0) $ Just s
                  | r >= 9      = Just s
                  | otherwise   = case s M.! Tile r c of
                        E list -> let
                                    tile = Tile r c
                                    test n = go (Tile r $ c + 1) $
                                                fixTile tile $ M.insert tile (D n) s
                                  in msum $ map test list
                        D n -> go (Tile r $ c + 1) (Just s)
            \end{lstlisting}

            The function just call the internal \texttt{go} function with the
            starting point, the upper left tile. This function is split into 5
            cases:
            \begin{enumerate}
                \item \textbf{line 5:} If we don't get a Sudoku, we can't just
                    make up a Sudoku, so we return \texttt{Nothing}.
                \item \textbf{line 7:} If our walker goes past the last column,
                    we continue in the next row.
                \item \textbf{line 8:} If we're past the last row, we've
                    completed the Sudoku, so we return \texttt{Just} that.
                \item \textbf{line 15:} If the current tile points at a decided
                    digit, we continue with the next.
                \item \textbf{line 9-14:} The current tile points a a list of
                    possibilities. We insert each of these possibilities as a
                    decided digit, fix it with \texttt{fixTile}, and continue on
                    the next tile. \texttt{msum} takes this list of
                    \texttt{Maybe Sudoku}'s, and returns the first that is not
                    \texttt{Nothing}. Thanks to Haskell's laziness, only the
                    first \texttt{Just} Sudoku will be calculated.
            \end{enumerate}
            
        \end{subsubsection}

        
        \begin{subsubsection}*{The main method}

            Holding this all together is the main method:
            \begin{lstlisting}
            main :: IO ()
            main = do
                times <- readLn :: IO Int
                replicateM_ times $ do
                    sudoku <- parseSudoku
                    print $ solveSudoku (fillSudoku sudoku)
                    _ <- getLine
                    putStrLn ""
            \end{lstlisting}

            This will read an integer from the first line of input, and parse
            and solve that many Sudoku's. Note that each of these Sudoku's
            should be followed by one unparsed line.

        \end{subsubsection}

    \end{subsection}



    \begin{subsection}*{Counting Function Calls}

        In stead of counting the 5 most common called functions, I've counted
        the 3 most interesting functions. I am aware this is not according to
        the assignment, but the five most called functions were not interesting
        at all, apart from the fact that I've got only 5 top level functions, not
        counting the main function.

        I've decided to count \texttt{fixTile}, \texttt{nextIteration} and
        \texttt{go}. The source code for explicit state and declarative counting
        can be found in \texttt{explicit-count.hs} and
        \texttt{declarative-count.hs} respectively. I won't go into the details
        of the changes here, just mention the constructs I used.

        For the explicit state, I've used a Haskell built-in named
        \texttt{IORef}. This can be viewed as a cell of memory in the
        \textit{world} of the IO Monad. This world is an state which is passed
        around implicitly. You still have to pass around the reference to this
        cell.

        Withing the declarative model, I've added extra parameters to the
        functions, to pass in and return (after increasing) the count.

        Now as for which of these I prefer, I'd say the declarative way. Even
        though the stateful method is more concise, it's considered bad practice
        in Haskell to use it a lot. This is because it can introduce strange
        bugs to your code, as functions will no longer return the same result
        for the same parameters. If it is used well, explicit state can simplify
        your code a lot, though.

        I must mention here that there seems to be a bug in the either counting
        method, because they claim different results.

    \end{subsection}


\end{section}



\begin{section}*{Assignment 2 - Introducing Concurrency}


    \begin{subsection}*{Choices of data structures}

        I had to make very little changes to the data structure to enable
        concurrent programming. Instead of mapping tiles on cells, I've mapped
        tiles on a sort of locked cell, called \texttt{MVar} in Haskell.

        \texttt{MVar}'s aren't actual locks, but can be used that way. They are
        either empty (taking, locked) or full (released). By putting a part of
        code between a \texttt{takeMVar} and a \texttt{putMVar}, you can make it
        exclusive. Further, \texttt{takeMVar} returns the value currently kept
        in the \texttt{Mvar}, while \texttt{putMVar} will store a new value. You
        can also use \texttt{modifyMVar}, which takes a function to atomically
        modify the value held by the \texttt{MVar}. At last, \texttt{takeMVar}
        and \texttt{modifyMVar} will wait for the \texttt{MVar} to be full.

        From this explanation, it's probably clear that I mean to use the
        stateful concurrent model.

        \begin{lstlisting}
        data Cell = E [Int] | D Int | X
            deriving (Eq)
        data Tile = Tile { getR :: Int, getC :: Int }
            deriving (Eq, Show, Ord)
        type Sudoku = M.Map Tile (MVar Cell)
        data Samurai = Samurai { getUL :: Sudoku
                               , getUR :: Sudoku
                               , getLL :: Sudoku
                               , getLR :: Sudoku
                               , getCE :: Sudoku }
        \end{lstlisting}

    \end{subsection}


    \begin{subsection}*{Choices of algorithms}

        Because backtracking would mean an awful lot of communication between
        the threads, which significantly complexes programming, I've fallen
        back to human solving strategies.

        I've implemented a number of strategies found either by myself or with
        help of the web. Using human strategies, of course, means that I cannot
        solve every possible Sudoku, though most are within the league of my
        program. All of these techniques work on the possibilities of digits for
        a certain cell, by scratching the contradictions. The techniques I've
        used use listed below:

        \begin{itemize}
            \item \textbf{Remove clashes:} This is probably the most obvious
                technique, and I've used it in the single Sudoku solver as well:
                For each decided digit, remove that digit from the other cells
                in the row, column or box.
            \item \textbf{These cells are belong to us:} This is a combination
                of several strategies found on the internet. Most sources on the
                internet mention ``single position'' (there's only one position
                for this digit in the entire row, column or box), ``hidden
                pairs'' (the cells A and B both have (among others)
                possibilities 1 and 2, and the 1 and 2 do not occur elsewhere
                within this row/column/box, then we can exclude those others),
                ``hidden triples'', ... These can be combined into following
                statement: ``If n possibilities are limited to n cells within
                one row, column or box, no other digits than these n can occur
                here.''
            \item \textbf{We stand united:} This, too, is a combination of web
                strategies. They are called ``naked pairs'', ``naked triples''
                and so on. Naked pairs states that when two cells have the same
                two possibilities (and no other), these possibilities cannot
                occur in other cells of the same row, column or box, as they are
                required to fill the pair. The same can be applied to any number
                of cells: ``If n cells in the same row, column or box are
                limited to the same n possibilities, these possibilities can be
                scratched from the other cells in this row, column or box.''
            \item \textbf{Candidate lines:} If within a box, all possible
                locations for a digit occur on one row or column, that digit
                cannot occur elsewhere in that row or column.
        \end{itemize}

    \end{subsection}


    \begin{subsection}*{Choice of concurrency model}

        I've chosen for the stateful concurrency model. This models allows me to
        completely separate the threads in an abstraction. I can use the
        \texttt{MVar}'s as normal variables as long as I lock them, without the
        need to think of what should happen first, ...

        The way I've implemented it, the only thing threads do it scratching
        possibilities, and filling in the variables when they're decided. If
        between two call of a function, a possibility suddenly disappears, this
        will give no trouble at all. Therefore, the threads only synchronize
        when they notice they've stopped changing.

        In case of message passing concurrency, I'd have to stop calculating and
        check if any of the other threads made a change more regular. The
        advantage would be, that message passing is more predictable. The cell
        content wouldn't suddenly change, we'd consciently read the changes from
        the messages. That's an advantage if you don't like the complexity of
        locking, but requires more code.

        Declarative concurrency wouldn't be very practical here, because we need
        quite a lot of communication, and variables keep changing all the time.
        We couldn't use dataflow variables for that, as they are monotonic. Our
        threads wouldn't be able to run independently, which is another
        disadvantage.

    \end{subsection}


    \begin{subsection}*{The concurrent code}

        In this part, I'll explain the code I've used for the concurrent
        implementation. I won't go through every part. For one, I'll skip
        parsing, as the only difference with parsing ordinary Sudoku's is the
        technical difficulty of using the \texttt{MVar}'s. Note that I've also
        included a printer (\texttt{printSamurai}), which prints a nice Samurai
        at it's current state, and a debug printer (\texttt{printSamurai'}),
        which prints all possibilities for each cell.

        This time, I'll start with the main method. The implementations of the
        strategies are found below.

        \begin{subsubsection}*{The main thread}

            The main thread is the thread that starts the other threads to solve
            the Sudoku's in the corner, and solves the center Sudoku itself.

            \begin{lstlisting}
            main :: IO ()
            main = do
                initialSamurai <- parseSamurai
                solvedSamurai  <- samuraiLooper initialSamurai
                printSamurai solvedSamurai
            \end{lstlisting}

            It makes use of several subfunctions. First is parses the samurai
            from standard in, then it solve it in a loop, after which it prints.
            The \texttt{samuraiLooper} is responsible for the synchronization
            of the threads.

            \begin{lstlisting}
            samuraiLooper :: Samurai -> IO Samurai
            samuraiLooper (Samurai ul ur ll lr ce) = do
                locks <- replicateM 5 $ newMVar True
                let [ullock, urlock, lllock, lrlock, celock] = locks
                _ <- forkIO $ sudokuLooper ullock ul
                _ <- forkIO $ sudokuLooper urlock ur
                _ <- forkIO $ sudokuLooper lllock ll
                _ <- forkIO $ sudokuLooper lrlock lr
                sudokuLooper celock ce
                changes <- mapM takeMVar locks
                if or changes
                    then samuraiLooper $ Samurai ul ur ll lr ce
                    else return (Samurai ul ur ll lr ce)
            \end{lstlisting}

            In this function, we create 5 \texttt{MVar Bool}'s. These are locks
            which contain a Boolean value. In our case, this Bool indicates
            whether the Sudoku changed. Initially, these are all true. Then, we
            call the \texttt{sudokuLooper} function in 4 different threads, each
            with a lock and a cornersudoku. We call the \texttt{sudokuLooper}
            for the center Sudoku in this thread.

            Next, using the locks as a semaphore, we wait until every thread has
            placed a Bool into it's lock. If none of the threads has changed
            anything, we return the samurai as the final solution. Otherwise, we
            call this function recursively.

            \begin{lstlisting}
            sudokuLooper :: MVar Bool -> Sudoku -> IO ()
            sudokuLooper lock sudoku = do
                _ <- takeMVar lock
                (_, change) <- solveSudokuStep (sudoku, True)
                _ <- untilM (not . snd) solveSudokuStep (sudoku, change)
                putMVar lock change
            \end{lstlisting}

            The loop for a single Sudoku happens in \texttt{sudokuLooper}. First
            of all, we take the lock on this Sudoku, so the main thread has to
            wait on us. Then, we execute a single solution step. We remember
            whether this step changed anything. We then loop these solution steps
            until no more change occurs. At last, we release the lock, by
            putting in whether we changed anything or not.

            \begin{lstlisting}
            solveSudokuStep :: (Sudoku, Bool) -> IO (Sudoku, Bool)
            solveSudokuStep (sudoku, _) = do
                sudoku' <- removeClashes sudoku >>= weStandUnited >>= theseCellsAreBelongToUs >>= candidateLine
                fixSingles sudoku'
            \end{lstlisting}

            And at last, the \texttt{solveSudokuStep} function calls each
            strategy once on the Sudoku, and decides the cells with only one
            possibility left.

        \end{subsubsection}

        \begin{subsubsection}*{Remove clashes}

            \begin{lstlisting}
            removeClashes :: Sudoku -> IO Sudoku
            removeClashes sudoku = F.foldrM fixTile sudoku (M.keys sudoku)
              where
                fixTile :: Tile -> Sudoku -> IO Sudoku
                fixTile (Tile r c) _ = F.foldrM (filterTile $ Tile r c) sudoku (M.keys sudoku)

                filterTile :: Tile -> Tile -> Sudoku -> IO Sudoku
                filterTile fixed tofix _ = if clashingTiles fixed tofix
                    then do
                        fixed_cell <- readMVar $ sudoku M.! fixed
                        modifyMVar_ (sudoku M.! tofix) (filterCell fixed_cell)
                        return sudoku
                    else return sudoku

                filterCell :: Cell -> Cell -> IO Cell
                filterCell (D n) (E ds) = return $ E $ filter (/= n) ds
                filterCell _ t          = return t
            \end{lstlisting}

        \end{subsubsection}


        \begin{subsubsection}*{These cells are belong to us}

            \begin{lstlisting}
            theseCellsAreBelongToUs :: Sudoku -> IO Sudoku
            theseCellsAreBelongToUs sudoku = F.foldrM go sudoku parts
              where
                go :: [Tile] -> Sudoku -> IO Sudoku
                go part _ = filterOnLength <$> patterns >>= write
                  where
                    patterns :: IO (Map [Int] [Int])
                    patterns = F.foldlM insertIndices empty [1..9]

                    -- Pairs op list of indices and the digits on those indices.
                    insertIndices :: Map [Int] [Int] -> Int -> IO (Map [Int] [Int])
                    insertIndices m n = do
                        is <- indices n part sudoku
                        return $ M.alter (Just . (n:) . F.concat) is m

                    -- Where the list of indices has the same length as the digits on those
                    -- indices.
                    filterOnLength :: Map [Int] [Int] -> Map [Int] [Int]
                    filterOnLength = M.filterWithKey ((==) `on` length)

                    -- Write the digits on the indices.
                    write :: Map [Int] [Int] -> IO Sudoku
                    write m = do
                        F.mapM_ (\\(k, v) ->
                                    F.mapM_ (\i ->
                                        modifyMVar_ (sudoku M.! (part !! i)) $ \cell -> case cell of
                                            D d -> return $ D d
                                            E _ -> return $ E v
                                            X   -> return X
                                    ) k
                                ) $ M.toList m
                        return sudoku
            \end{lstlisting}

        \end{subsubsection}


        \begin{subsubsection}*{We stand united}

            \begin{lstlisting}
            weStandUnited :: Sudoku -> IO Sudoku
            weStandUnited sudoku = F.foldlM go sudoku parts
              where
                go :: Sudoku -> [Tile] -> IO Sudoku
                go _ part = filterOnLength <$> (F.foldlM insertPossibilities first $ zip part [0..]) >>= write
                  where
                    first = M.singleton [] [] :: Map [Int] [Int]

                    insertPossibilities :: Map [Int] [Int] -> (Tile, Int) -> IO (Map [Int] [Int])
                    insertPossibilities m (tile, i) = do
                        cell <- readMVar (sudoku M.! tile)
                        let x = case cell of
                                D _ -> m
                                E d -> M.fromListWith merge $ concatMap (\(k, v) -> posses k v i $ merge k d) $ M.toList m
                                X   -> m
                        return x

                    --
                    posses :: [Int] -> [Int] -> Int -> [Int] -> [([Int], [Int])]
                    posses k1 is i k2
                      | k1 == k2  = [(k1, merge [i] is)]
                      | otherwise = [(k1, is), (k2, merge [i] is)]

                    -- Where the list of indices has the same length as the digits on those
                    -- indices.
                    filterOnLength :: Map [Int] [Int] -> Map [Int] [Int]
                    filterOnLength = M.filterWithKey ((==) `on` length)

                    write :: Map [Int] [Int] -> IO Sudoku
                    write = F.foldrM scratch sudoku . M.toList

                    scratch :: ([Int], [Int]) -> Sudoku -> IO Sudoku
                    scratch (ps, is) _ = F.foldrM (\i _ -> do
                            let mvar = (sudoku M.! (part !! i))
                            cell <- takeMVar mvar
                            putMVar mvar $ case cell of
                                E d -> E $ d \\ ps
                                x   -> x
                            return sudoku
                        ) sudoku $ [0..8] \\ is
            \end{lstlisting}

        \end{subsubsection}


        \begin{subsubsection}*{Candidate lines}

            \begin{lstlisting}
            candidateLine :: Sudoku -> IO Sudoku
            candidateLine sudoku = F.foldrM go sudoku blocks
              where
                go :: [Tile] -> Sudoku -> IO Sudoku
                go block _ = do
                    (h, v) <- rowcolWise block
                    mapM_ (scratch (\r -> filter (not . inRow) [Tile r c | c <- [0..8]])) $ M.toList $ toIndexDigits $ onlyOne h
                    mapM_ (scratch (\c -> filter (not . inCol) [Tile r c | r <- [0..8]])) $ M.toList $ toIndexDigits $ onlyOne v
                    return sudoku
                  where
                    rbounds = ( minimum $ map getR block, maximum $ map getR block )
                    cbounds = ( minimum $ map getC block, maximum $ map getC block )
                    inCol (Tile r _) = fst rbounds <= r && r <= snd rbounds
                    inRow (Tile _ c) = fst cbounds <= c && c <= snd cbounds

                    scratch :: (Int -> [Tile]) -> (Int, [Int]) -> IO ()
                    scratch tiles (i, ds) = mapM_
                        (\t -> modifyMVar_ (sudoku M.! t)
                                (\cell -> return $ case cell of
                                    E d -> E $ d \\ ds
                                    x   -> x
                                )
                        ) $ tiles i
                
                -- Maps each index on the rows/cols it's on.
                rowcolWise :: [Tile] -> IO (Map Int [Int], Map Int [Int])
                rowcolWise block = F.foldrM
                    (\tile (h, v) -> do
                        cell <- readMVar (sudoku M.! tile)
                        return $ case cell of
                            D d  -> (ins tile (getR) h d, ins tile (getC) v d)
                            E ds -> (foldl (ins tile $ getR) h ds, foldl (ins tile $ getC) v ds)
                            X    -> (h, v)
                    ) (empty, empty) block
                  where ins t f m d = M.alter (Just . (merge [f t]) . F.concat) d m

                onlyOne :: Map Int [Int] -> Map Int Int
                onlyOne = M.mapMaybe (\x -> if length x == 1 then Just (head x) else Nothing)

                toIndexDigits :: Map Int Int -> Map Int [Int]
                toIndexDigits = (M.fromListWith (merge)) . (map (\\(d, i) -> (i, [d]))) . M.toList
            \end{lstlisting}

        \end{subsubsection}


    \end{subsection}

\end{section}

\end{document}

